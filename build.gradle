buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'

apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${mod_version}-${mod_phase}"
group = "${mod_group}"
archivesBaseName = 'After the Drizzle'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: "${mappings_channel}", version: "${mappings_version}"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'info'

            mods {
                afterthedrizzle {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'info'

            mods {
                afterthedrizzle {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'info'

            args '--mod', 'afterthedrizzle', '--all', '--output', file('src/generated/resources/')

            mods {
                afterthedrizzle {
                    source sourceSets.main
                }
            }
        }
    }
}

def info = [
                "Specification-Title": "afterthedrizzle",
                "Specification-Vendor": "Lemon Slice Studio",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"Lemon Slice Studio",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
]

repositories {
    maven {
        name = "silveroak"
        url = "http://maven.lemonslice.cloud/releases"
    }
    maven {
        name = "jei"
        url = "https://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compile fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")
    compile fg.deobf("cloud.lemonslice.silveroak:SilveroakOutpost-Forge-1.15.2:0.2.3-Alpha")
}

jar {
    manifest {
        attributes(info)
    }
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "cloud.lemonslice.afterthedrizzle"
            artifactId = "AfterTheDrizzle-Forge-${minecraft_version}"

            artifact reobfArtifact
            pom {
                name = 'After the Drizzle for Minecraft 1.15.2'
                description = 'A Minecraft agricultural mod library.'
                url = 'https://github.com/lemon-slice-studio/After-the-Drizzle'
                licenses {
                    license {
                        name = 'BSD-3-Clause'
                        url = 'https://github.com/lemon-slice-studio/After-the-Drizzle/blob/1.15.2/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'RoitoLeo'
                        name = 'RoitoLeo'
                    }
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/lemon-slice-studio/After-the-Drizzle/issues'
                }
                scm {
                    url = 'https://github.com/lemon-slice-studio/After-the-Drizzle'
                    connection = 'scm:git:git://github.com/lemon-slice-studio/After-the-Drizzle.git'
                    developerConnection = 'scm:git:git@github.com:lemon-slice-studio/After-the-Drizzle.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = "archive"
            url = "http://maven.lemonslice.cloud/releases"
            credentials {
                username = System.env.MAVEN_USERNAME
                password = System.env.MAVEN_PASSWORD
            }
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    onlyIf {
        System.env.MAVEN_USERNAME && System.env.MAVEN_PASSWORD
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}